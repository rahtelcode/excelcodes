//RECURSIVEFILTER AND ExtractColumns two function's wrapper to filter rows with first one and fetch desired columns with second one

FILTER.ROWS.COLUMNS	
=LAMBDA(
	tableall,criteriarange,columnnames,[withheader],
    	LET(
		tbl,tableall,
		tbla,ARRAY.PUSH(RECFILTERWRAPER(tbl,criteriarange),INDEX(tbl,1,)),
		ExtractColumns(tbla, columnnames, withheader)
	)
)
  
//Push array one row down with extra one row to the top. Tow row with default value e.g. default={value1,value2,…}	

ARRAY.PUSH (WITH EXTRA ROW)	
=LAMBDA(Array,Defaults,
  	LET(D,IF(COLUMNS(Defaults)>1,
        	INDEX(Defaults,1,0),TRANSPOSE(Defaults)),
      		X,SEQUENCE(1,COLUMNS(Array)), 
     		Y,SEQUENCE(ROWS(Array)+1,1,0), 
       		IF(Y<1,IFERROR(INDEX(D,1,X),""""),INDEX(Array,Y,X))
	)

)

//Push array one row down array size not changed. Last row removed. Top row with default values. E.g. default={value1,value2,…}

ARRAY.PUSH (WITHOUT EXTRA ROW)	
=LAMBDA(Array,Defaults,
  LET(D,IF(COLUMNS(Defaults)>1,
        INDEX(Defaults,1,0),TRANSPOSE(Defaults)),
      X,SEQUENCE(1,COLUMNS(Array)), 
     Y,SEQUENCE(ROWS(Array),1,0), 
       IF(Y<1,IFERROR(INDEX(D,1,X),""""),INDEX(Array,Y,X))))
 
 
//Wraper function for RECURSIVEFILTER Function	
RECFILTERWRAPER	
=LAMBDA(tableall,criteriarange,
	LET(
		tbl,tableall,
		hd,INDEX(tableall,1,),
		filterrange,FILTER(criteriarange,ISBLANK(INDEX(criteriarange,2,)) = FALSE),
		filterhd,INDEX(filterrange,1,),
		filterval,INDEX(filterrange,2,),
		RECURSIVEFILTER(INDEX(tableall,2,1):INDEX(tableall,ROWS(tbl),COLUMNS(tableall)),filterhd,filterval)
	)
)	


